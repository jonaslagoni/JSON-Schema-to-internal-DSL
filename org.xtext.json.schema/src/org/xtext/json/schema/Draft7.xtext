grammar org.xtext.json.schema.Draft7 with org.eclipse.xtext.common.Terminals
generate draft7 "http://www.xtext.org/json/schema/Draft7"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
// inspired by github.com/SOM-Research/asyncapi-toolkit
Schema:
	{Schema}
	'{'
		(
			('"type"' ':' type=Types ','?)?
			&('"dependencies"' ':' '{' dependencies=Dependencies '}' ','?)?
			&('"additionalProperties"' ':' additionalProperties=AdditionalProperties ','?)?
			&('"additionalItems"' ':' additionalItems=AdditionalItems ','?)?
			&('"default"' ':' default=Default ','?)?
			&('"patternProperties"' ':' '{' patternProperties+=NamedSchema (',' patternProperties+=NamedSchema)* '}' ','?)?
			&('"examples"' ':' example=Example ','?)?
			&('"items"' ':' items=Items ','?)?
			&('"contains"' ':' contains=Contains ','?)?
			&('"definitions"' ':' '{' definitions+=NamedSchema (',' definitions+=NamedSchema)* '}' ','?)?
			&('"title"' ':' title=AnyString ','?)?
			&('"required"' ':' '[' requiredProperties+=[NamedSchema|AnyString] (',' requiredProperties+=[NamedSchema|AnyString])* ']' ','?)?
			&('"description"' ':' description=AnyString ','?)?
			&('"properties"' ':' '{' properties+=NamedSchema (',' properties+=NamedSchema)* '}' ','?)?
			&('"enum"' ':'  '[' enumValues+=AnyString (',' enumValues+=AnyString)* ']' ','?)?
			&('"$schema"' ':' '"http://json-schema.org/draft-07/schema#"' ','?)?
			&('"$id"' ':' id=AnyString ','?)?
			&('"$comment"' ':' comment=AnyString','?)?
			&('"if"' ':' ifSchema=AbstractSchema ','?)?
			&('"then"' ':' thenSchema=AbstractSchema ','?)?
			&('"else"' ':' elseSchema=AbstractSchema ','?)?
			&('"enum"' ':'  '[' enumValues+=AnyString (',' enumValues+=AnyString)* ']' ','?)?
			&('"const"' ':' (const=AnyString | constBoolean=Boolean) ','?)?
			&('"anyOf"' ':' '[' anyOfs+=AbstractSchema (',' anyOfs+=AbstractSchema)* ']' ','?)?
			&('"oneOf"' ':' '[' oneOfs+=AbstractSchema (',' oneOfs+=AbstractSchema)* ']' ','?)?
			&('"allOf"' ':' '[' allOfs+=AbstractSchema (',' allOfs+=AbstractSchema)* ']' ','?)?
			&('"not"' ':' not=AbstractSchema ','?)?
			&('"uniqueItems"' ':' unique=Boolean ','?)?
			&('"propertyNames"' ':' propertyNames=AbstractSchema ','?)?
			&('"minProperties"' ':' minProperties=INT ','?)?
			&('"maxProperties"' ':' maxProperties=INT ','?)?
			&('"maxItems"' ':' maxItems=INT ','?)?
			&('"minItems"' ':' minItems=INT ','?)?
			&('"maxLength"' ':' maxLength=INT ','?)?
			&('"minLength"' ':' minLength=INT ','?)?
			&('"pattern"' ':' pattern=AnyString ','?)?
			&('"format"' ':' format=FormatTypes ','?)?
			&('"contentMediaType"' ':' mediaType=AnyString ','?)?
			&('"contentEncoding"' ':'  encoding=AnyString ','?)?
			&('"multipleOf"' ':' multipleOf=Number ','?)?
			&('"minimum"' ':' minimum=Number ','?)?
			&('"exclusiveMinimum"' ':' exclusiveMinimum=Number ','?)?
			&('"maximum"' ':' maximum=Number ','?)?
			&('"exclusiveMaximum"' ':' exclusiveMaximum=Number ','?)?
		)
	'}'
;
Dependencies:
	propertyDependencies+=PropertyDependency (',' propertyDependencies+=PropertyDependency)*
	| (schemaDependencies+=CrossReferencedDependencyNamedSchema (',' schemaDependencies+=CrossReferencedDependencyNamedSchema)*)
;
CrossReferencedDependencyNamedSchema:
	name=[NamedSchema|AnyString] ':' schema=AbstractSchema
;
PropertyDependency:
	name=[NamedSchema|AnyString] ':' '[' dependencies+=[NamedSchema|AnyString] (',' dependencies+=[NamedSchema|AnyString])* ']'
;
Contains:
	('[' containSchemas+=AbstractSchema (',' containSchemas+=AbstractSchema)* ']') | containSchemas+=AbstractSchema
;
Items:
	('[' items+=AbstractSchema (',' items+=AbstractSchema)* ']') | items+=AbstractSchema | selfReference=Boolean
;
AdditionalProperties:
	allowedBoolean=Boolean | schema=AbstractSchema
;
AdditionalItems:
	allowedBoolean=Boolean | schema=AbstractSchema
;
Default:
	 defaultSchema=AbstractSchema | defaultString=AnyString | defaultBoolean=Boolean | defaultNumber=Number | {Default} ('[' (defaultArrayItems+=AbstractSchema (',' defaultArrayItems+=AbstractSchema)*)? ']')
;
Example:
	exampleSchema=AbstractSchema | exampleString=AnyString | exampleBoolean=Boolean | exampleNumber=Number | ('[' exampleArrayItems+=AbstractSchema (',' exampleArrayItems+=AbstractSchema)* ']')
;
Types:
	('[' jsonTypes+=JsonTypes (',' jsonTypes+=JsonTypes)* ']') | jsonTypes+=JsonTypes
;
AbstractSchema:
	Reference | Schema
;
Reference:
	'{' '"$ref"' ':' schemaRef=AnyString '}'
;


NamedSchema:
	name=AnyString ':' schema=AbstractSchema
;




enum Boolean:
	UNSET
	| trueValue='true' 
	| falseValue='false'
;

enum JsonTypes: 
	UNSET
	| string='"string"'
	| integer='"integer"'
	| number='"number"'
	| object='"object"'
	| array='"array"'
	| boolean='"boolean"'
	| null='"null"'
;

enum FormatTypes:
	UNSET |
	date_time='"date-time"' | 
	time='"time"' | 
	date='"date"' | 
	email='"email"' | 
	idn_email='"idn-email"' | 
	hostname='"hostname"' | 
	ipv4='"ipv4"' | 
	ipv6='"ipv6"' | 
	uri='"uri"' | 
	uri_reference='"uri-reference"' | 
	iri='"iri"' | 
	iri_reference='"iri-reference"' | 
	uri_template='"uri-template"' | 
	json_pointer='"json-pointer"' | 
	relative_json_pointer='"relative-json-pointer"' | 
	regex='"regex"'
;

terminal ENUMBER:
   ('E' | 'e') '-'? INT;
Number:
	(negative='-')? number=INT ('.' decimal=INT)? (exponent=ENUMBER)?
;

AnyString: STRING | JsonSchemaKeyword;


JsonSchemaKeyword:
	'"title"'
	| '"http://json-schema.org/draft-07/schema#"'
	| '"additionalProperties"'
	| '"additionalItems"'
	| '"description"'
	| '"$comment"'
	| '"$id"'
	| '"definitions"'
	| '"default"'
	| '"examples"'
	| '"$schema"'
	| '"$ref"'
	| '"enum"'
	| '"const"'
	| '"type"'	
	| '"string"'
	| '"integer"'
	| '"number"'
	| '"object"'
	| '"array"'
	| '"boolean"'
	| '"null"'
	| '"date-time"'
	| '"time"'
	| '"date"'
	| '"email"'
	| '"idn-email"'
	| '"hostname"'
	| '"ipv4"'
	| '"ipv6"'
	| '"uri"'
	| '"uri-reference"'
	| '"iri"'
	| '"iri-reference"'
	| '"uri-template"'
	| '"json-pointer"'
	| '"relative-json-pointer"'
	| '"regex"'
	| '"contains"'
	| '"uniqueItems"'
	| '"propertyNames"'
	| '"length"'
	| '"minLength"'
	| '"maxLength"'
	| '"minItems"'
	| '"maxItems"'
	| '"pattern"'
	| '"format"'
	| '"minProperties"'
	| '"maxProperties"'
	| '"dependencies"'
	| '"patternProperties"'
	| '"required"'
	| '"properties"'
	| '"if"'
	| '"then"'
	| '"else"'
	| '"anyOf"'
	| '"oneOf"'
	| '"allOf"'
	| '"not"'	
	| '"items"'
	| '"contentMediaType"'
	| '"contentEncoding"'
	| '"multipleOf"'
	| '"minimum"'
	| '"exclusiveMinimum"'
	| '"maximum"'
	| '"exclusiveMaximum"';